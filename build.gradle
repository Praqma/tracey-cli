apply plugin: 'java'
apply plugin: 'wrapper'

sourceCompatibility = '1.7'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

def homeDir = System.properties['user.home']

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
    compile('com.github.Praqma:tracey-broker:master-SNAPSHOT') { changing = true }
    compile 'commons-cli:commons-cli:1.3.1'
    compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.6'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task uberjar(type: Jar, dependsOn: [':compileJava', ':processResources']) {
    from files(sourceSets.main.output.classesDir)
    from(configurations.runtime.asFileTree.files.collect { zipTree(it) }) {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }

    manifest {
        attributes 'Main-Class': 'net.praqma.tracey.cli.Tracey'
    }
}

task installLocally(type: Copy, dependsOn: ['uberjar']) {
    from "${project.projectDir}/build/libs"
    into "${homeDir}/Tracey"
}
